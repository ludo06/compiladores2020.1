@@grammar::IMP
@@eol_comments :: /#.*?$/

@@keyword :: 'and'
@@keyword :: 'not'
@@keyword :: 'or'
@@keyword :: 'True'
@@keyword :: 'False'
@@keyword :: '+'
@@keyword :: '-'
@@keyword :: '(' ')'
@@keyword :: '=='
@@keyword :: '<'
@@keyword :: '<='
@@keyword :: '>'
@@keyword :: '>='
@@keyword :: ':='
@@keyword :: 'while' 
@@keyword :: 'if' 'then' 'else' 
@@keyword :: 'var' '='
@@keyword :: 'const' '='
@@keyword :: ';'
@@keyword :: 'skip'
@@keyword :: '{' '}'
@@keyword :: 'print'
@@keyword :: 'return'

start = [ds:decSeq] [cs:cmd_seq] $ ;

exp = call | tamanho_lista | lista_indice | lista_append | paren_exp | bin_exp | un_exp | lista_inteiros | @:atom ;

lista_indice = idn:identifier"["e:exp"]" ;

lista_append = lista1:lista_inteiros "++" lista2:lista_inteiros | lista1:lista_inteiros "++" lista2:exp | lista1:exp "++" lista2:lista_inteiros | lista1:exp "++" lista2:exp ;

lista_inteiros = "[" e:exp {','e:exp}* "]" ;

associa_lista = idn:identifier"["idx:exp"]" op:":=" e:exp ;

tamanho_lista = "$" idn:identifier ;

comando_return = op:"return" e:exp;

paren_exp = "(" e:exp ")" ;

bin_exp = lista:lista_inteiros op:binop e:exp | e1:exp op:binop e2:exp ;

un_exp = op:"not" e:exp ;

atom = @:num_atom  | @:truth_atom | @:id_atom ;

num_atom = number ;

truth_atom = truth ;

id_atom = identifier ;

binop = "conc" | "and"| "or" | "==" | "<=" | ">=" | "<" | ">"  | "+" | "-" | "*" | "/" ;

truth = 'True' | 'False' ;

# This makes sure that an identifier does not match a keyword.
@name
identifier = /(?!\d)\w+/ ;

number = /\d+/ ;

blk = "{" ds:decSeq cs:cmd_seq "}" ;

cmd_seq = ";"%{ac:atom_cmd}+ ;

atom_cmd = associa_lista | cond | loop | assign | print | call | skip | comando_return ;

skip = "skip" ;

assign = idn:identifier op:":=" e:exp ;

print = op:"print" e:exp ;

cond = op:"if" t:paren_exp b1:blk ["else" b2:blk] ; 

loop = op:"while" t:paren_exp b:blk ;

dec = fn | rec | var | const ;

decSeq = { d:dec }* ;

var = op:"var" ","%{ idn:identifier "=" e:exp }+ ";" ;

const = op:"const" idn:identifier "=" e:exp ";" ;

fn = op:"def" idn:identifier '(' f:formal ')' b:blk ;

rec = op:"rec" idn:identifier '(' f:formal ')' b:blk ;

formal = ','%{ identifier }* ;

call = idn:identifier '(' a:actual ')' ;

actual = ','%{ exp }* ;



